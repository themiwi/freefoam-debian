
#-------------------------------------------------------------------------------
#               ______                _     ____          __  __
#              |  ____|             _| |_  / __ \   /\   |  \/  |
#              | |__ _ __ ___  ___ /     \| |  | | /  \  | \  / |
#              |  __| '__/ _ \/ _ ( (| |) ) |  | |/ /\ \ | |\/| |
#              | |  | | |  __/  __/\_   _/| |__| / ____ \| |  | |
#              |_|  |_|  \___|\___|  |_|   \____/_/    \_\_|  |_|
#
#                   FreeFOAM: The Cross-Platform CFD Toolkit
#
# Copyright (C) 2008-2012 Michael Wild <themiwi@users.sf.net>
#                         Gerber van der Graaf <gerber_graaf@users.sf.net>
#-------------------------------------------------------------------------------
# License
#   This file is part of FreeFOAM.
#
#   FreeFOAM is free software: you can redistribute it and/or modify it
#   under the terms of the GNU General Public License as published by the
#   Free Software Foundation, either version 3 of the License, or (at your
#   option) any later version.
#
#   FreeFOAM is distributed in the hope that it will be useful, but WITHOUT
#   ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
#   FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
#   for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with FreeFOAM.  If not, see <http://www.gnu.org/licenses/>.
#
#   THE FIRST LINE OF THIS FILE IS INTENTIONALLY LEFT BLANK!
#
#-------------------------------------------------------------------------------

set(SOURCE_DIR "@CMAKE_SOURCE_DIR@")
set(BINARY_DIR "@CMAKE_BINARY_DIR@")
set(FOAM_INSTALL_CONFIG_PATH "@FOAM_INSTALL_CONFIG_PATH@")
set(FOAM_UPSTREAM_VERSION_FULL "@FOAM_UPSTREAM_VERSION_FULL@")
set(GIT_EXECUTABLE "@GIT_EXECUTABLE@")

execute_process(COMMAND "${GIT_EXECUTABLE}" log -1 --pretty=format:%h
  WORKING_DIRECTORY "${SOURCE_DIR}"
  RESULT_VARIABLE result
  OUTPUT_VARIABLE output
  ERROR_VARIABLE output
  OUTPUT_STRIP_TRAILING_WHITESPACE
  ERROR_STRIP_TRAILING_WHITESPACE)

if(result)
  message(FATAL_ERROR "Failed to run obtain git hash:"
    ${output})
endif()

set(FOAM_BUILD_NUMBER ${output})

execute_process(COMMAND "${GIT_EXECUTABLE}" describe --dirty
  WORKING_DIRECTORY "${SOURCE_DIR}"
  RESULT_VARIABLE result
  OUTPUT_VARIABLE output
  ERROR_VARIABLE output
  OUTPUT_STRIP_TRAILING_WHITESPACE
  ERROR_STRIP_TRAILING_WHITESPACE)

if(result)
  message(FATAL_ERROR "Failed to run obtain git version:"
    ${output})
endif()

if(output MATCHES "v([0-9]+\\.[0-9]+)\\.(.+)")
  set(FOAM_VERSION "${CMAKE_MATCH_1}")
  set(FOAM_VERSION_FULL "${FOAM_VERSION}.${CMAKE_MATCH_2}")
else()
  message(FATAL_ERROR "Failed to parse version info from git-describe:"
    ${output})
endif()

configure_file("${SOURCE_DIR}/src/OpenFOAM/global/global.C.in"
  "${BINARY_DIR}/src/OpenFOAM/global/global.C" @ONLY)

# ------------------------- vim: set sw=2 sts=2 et: --------------- end-of-file
